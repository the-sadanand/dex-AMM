// SPDX-License-Identifier: MIT
pragma solidity ^0.8.19;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

contract DEX {

    address public tokenA;
    address public tokenB;

    uint256 public reserveA;
    uint256 public reserveB;

    uint256 public totalLiquidity;

    mapping(address => uint256) public liquidity;

    event LiquidityAdded(address indexed provider,uint256 amountA,uint256 amountB,uint256 liquidityMinted);
    event LiquidityRemoved(address indexed provider,uint256 amountA,uint256 amountB,uint256 liquidityBurned);
    event Swap(address indexed trader,address indexed tokenIn,address indexed tokenOut,uint256 amountIn,uint256 amountOut);

    constructor(address _tokenA,address _tokenB){
        tokenA=_tokenA;
        tokenB=_tokenB;
    }

    function addLiquidity(uint256 amountA,uint256 amountB) external returns(uint256 liquidityMinted){

        require(amountA>0 && amountB>0,"zero liquidity");

        IERC20(tokenA).transferFrom(msg.sender,address(this),amountA);
        IERC20(tokenB).transferFrom(msg.sender,address(this),amountB);

        if(totalLiquidity==0){

            liquidityMinted=sqrt(amountA*amountB);

        }else{

            uint256 liquidityA=(amountA*totalLiquidity)/reserveA;
            uint256 liquidityB=(amountB*totalLiquidity)/reserveB;

            liquidityMinted=liquidityA<liquidityB?liquidityA:liquidityB;
        }

        require(liquidityMinted>0,"invalid liquidity");

        liquidity[msg.sender]+=liquidityMinted;
        totalLiquidity+=liquidityMinted;

        reserveA+=amountA;
        reserveB+=amountB;

        emit LiquidityAdded(msg.sender,amountA,amountB,liquidityMinted);
    }

    function removeLiquidity(uint256 liquidityAmount) external returns(uint256 amountA,uint256 amountB){

        require(liquidity[msg.sender]>=liquidityAmount,"not enough liquidity");

        amountA=(liquidityAmount*reserveA)/totalLiquidity;
        amountB=(liquidityAmount*reserveB)/totalLiquidity;

        liquidity[msg.sender]-=liquidityAmount;
        totalLiquidity-=liquidityAmount;

        reserveA-=amountA;
        reserveB-=amountB;

        IERC20(tokenA).transfer(msg.sender,amountA);
        IERC20(tokenB).transfer(msg.sender,amountB);

        emit LiquidityRemoved(msg.sender,amountA,amountB,liquidityAmount);
    }

    function swapAForB(uint256 amountAIn) external returns(uint256 amountBOut){

        require(amountAIn>0,"zero swap");

        IERC20(tokenA).transferFrom(msg.sender,address(this),amountAIn);

        amountBOut=getAmountOut(amountAIn,reserveA,reserveB);

        require(amountBOut<reserveB,"insufficient liquidity");

        IERC20(tokenB).transfer(msg.sender,amountBOut);

        reserveA+=amountAIn;
        reserveB-=amountBOut;

        emit Swap(msg.sender,tokenA,tokenB,amountAIn,amountBOut);
    }

    function swapBForA(uint256 amountBIn) external returns(uint256 amountAOut){

        require(amountBIn>0,"zero swap");

        IERC20(tokenB).transferFrom(msg.sender,address(this),amountBIn);

        amountAOut=getAmountOut(amountBIn,reserveB,reserveA);

        require(amountAOut<reserveA,"insufficient liquidity");

        IERC20(tokenA).transfer(msg.sender,amountAOut);

        reserveB+=amountBIn;
        reserveA-=amountAOut;

        emit Swap(msg.sender,tokenB,tokenA,amountBIn,amountAOut);
    }

    function getPrice() external view returns(uint256 price){

        require(reserveA>0,"no reserves");

        price=(reserveB*1e18)/reserveA;
    }

    function getReserves() external view returns(uint256,uint256){

        return(reserveA,reserveB);
    }

    function getAmountOut(uint256 amountIn,uint256 reserveIn,uint256 reserveOut)
    public pure returns(uint256){

        require(amountIn>0,"invalid input");

        uint256 amountInWithFee=amountIn*997;

        uint256 numerator=amountInWithFee*reserveOut;

        uint256 denominator=(reserveIn*1000)+amountInWithFee;

        return numerator/denominator;
    }

    function sqrt(uint y) internal pure returns (uint z) {

        if (y > 3) {
            z = y;
            uint x = y / 2 + 1;
            while (x < z) {
                z = x;
                x = (y / x + x) / 2;
            }
        } else if (y != 0) {
            z = 1;
        }
    }
}